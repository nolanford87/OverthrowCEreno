private _warehouse = [player] call OT_fnc_nearestWarehouse;
if (isNull _warehouse) exitWith { hint "No warehouse near by!" };

params ["_range"];
private _found = "";
private _possible = [];
{
    private _d = _warehouse getVariable [_x, false];
    if (_d isEqualType []) then {
        _d params ["_cls", ["_num", 0, [0]]];
        if (_num > 0 && { _cls in OT_allOptics }) then {
            private _allModes = "true" configClasses (configFile >> "CfgWeapons" >> _cls >> "ItemInfo" >> "OpticsModes");
            private _max = 0;
            {
                _max = _max max getNumber (_x >> "distanceZoomMax");
            } forEach (_allModes);

            if (_max >= _range) then { _possible pushBack _cls };
        };
    };
} forEach ((allVariables _warehouse) select { ((toLowerANSI _x select [0, 5]) isEqualTo "item_") });

if (_possible isNotEqualTo []) then {
    private _sorted = [_possible, [], { (cost getVariable [_x, [200]]) select 0 }, "DESCEND"] call BIS_fnc_sortBy;
    _found = _sorted select 0;
};

_found;

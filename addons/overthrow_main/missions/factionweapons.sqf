params ["", "_jobparams"];
_jobparams params ["_faction"];

private _reppos = server getVariable [format ["factionrep%1", _faction], getPos player];
private _roads = _reppos nearRoads 75;
private _destination = [];
if (_roads isNotEqualTo []) then {
    _destination = getPos (_roads select 0);
} else {
    _destination = _reppos;
};

private _itemcls = selectRandom (OT_allBLURifles + OT_allBLUGLRifles + OT_allBLUMachineGuns);
private _itemName = _itemcls call OT_fnc_weaponGetName;
private _numitems = floor (5 + random 15);

private _params = [_destination, _faction, _itemcls, _numitems];
private _markerPos = _destination;
private _factionName = server getVariable format ["factionname%1", _faction];

//Build a mission description and title
private _description = format ["%1 requests %2 x %3. Deliver them to the marked location using any vehicle, just pull up with the weapons in the inventory and you will be paid for them, including any extras of the same type. </t><br/><br/><t size='0.9'>Reward: +5 (%1), export value of weapons", _factionName, _numitems, _itemName];
private _title = format ["%1 requests %2 x %3", _factionName, _numitems, _itemName];

//The data below is what is returned to the gun dealer/faction rep, _markerPos is where to put the mission marker, the code in {} brackets is the actual mission code, only run if the player accepts
[
    [_title, _description],
    _markerPos,
    {
        //No setup required for this mission
        true;
    },
    {
        //Fail check...
        false;
    },
    {
        //Success Check
        params ["_destination", "", "_itemcls", "_numitems"];
        private _numavailable = 0;
        {
            private _c = _x;
            if ((_x call OT_fnc_hasOwner) && (speed _x) < 0.1) exitWith {
                {
                    _x params ["_cls", "_amt"];
                    _cls = _cls call BIS_fnc_baseWeapon;
                    if (_cls == _itemcls) then {
                        _numavailable = _numavailable + _amt;
                    };
                } forEach (_c call OT_fnc_unitStock);
            };
        } forEach (_destination nearObjects ["AllVehicles", 30]);

        _numavailable >= _numitems;
    },
    {
        params ["_destination", "_faction", "_itemcls", "_numitems", "_wassuccess"];

        //If mission was a success
        if (_wassuccess) then {
            //Take the weapons and count them
            private _numavailable = 0;
            private _driver = objNull;
            {
                private _c = _x;
                if ((_x call OT_fnc_hasOwner) && (speed _x) < 0.1) then {
                    {
                        _x params ["_cls", "_amt"];
                        private _basecls = _cls call BIS_fnc_baseWeapon;
                        if (_basecls == _itemcls) then {
                            _driver = driver _c;
                            if (isNull _driver) then { _driver = (_c getVariable ["owner", ""]) call BIS_fnc_getUnitByUID }; // Nobody in the driver seat, pay the owner
                            // Pay only for weapons actually taken, the stock also counts backpacks etc. in the vehicle
                            _numavailable = _numavailable + ([_c, _cls, _amt] call OT_fnc_removeFromCargo);
                        };
                    } forEach (_c call OT_fnc_unitStock);
                };
            } forEach (_destination nearObjects ["AllVehicles", 30]);

            //apply standing and pay money
            private _topay = ([OT_nation, _itemcls, 0] call OT_fnc_getSellPrice) * _numavailable;
            [
                _topay,
                format [
                    "Delivered %1 x %2 (+5 %3)",
                    _numitems,
                    _itemcls call OT_fnc_weaponGetName,
                    server getVariable format ["factionname%1", _faction]
                ]
            ] remoteExec ["OT_fnc_money", _driver, false];
            server setVariable [format ["standing%1", _faction], (server getVariable [format ["standing%1", _faction], 0]) + 5, true];
        };
    },
    _params
];
